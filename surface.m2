--use notation from cmpart.pdf
F=ZZ/32003;
R=F[x,y,z];

--output: presentation of surface whose coordinate ring is generated by 
--P_s with parameters p,q,r with s=1,...,N
surf = (p,q,r,N) -> (
    S:=F[w_1..w_N,Degrees=>toList(1..N)];
    eqns := apply(N, i-> p*x^(i+1) + q*y^(i+1) + r*z^(i+1));
    f := map(R,S,eqns);
    ker f
    )

--compare reduced Hilbert series H1,H2
compare = (H1,H2) -> (numerator H1 == numerator H2) and (value denominator H1 == value denominator H2)

--output: smallest N such that P_(N+1) is in the subalgebra generated by P_1,...,P_N
guess = (p,q,r) -> (
    count:=2;
    H:=reduceHilbert hilbertSeries surf(p,q,r,1);
    H':=reduceHilbert hilbertSeries surf(p,q,r,2);
    while not compare(H,H') do (
	H=H';
	count=count+1;
	H'=reduceHilbert hilbertSeries surf(p,q,r,count);
	);
    count-1
    )

--assuming guess produces meaningful results, 
-- i.e., if P_(N+1) being generated by P_1,...,P_N implies the same for all P_j with j>N, 
-- then this returns true iff surface defined by p,q,r is CM
isCM = (p,q,r) -> (
    g:=guess(p,q,r);
    I:=surf(p,q,r,g);
    length res I == codim I
    )

end
restart
load "surface.m2"

isCM(1,2,3)--false
isCM(1,1,1)--true

